. This is Dataplot program file    WOMELDOR.DP
. Fit of refridgerant flame extinquishment data
.
. -----start point-----
.
DEVICE 1 X11
DEVICE 2 POSTSCRIPT
DIMENSION 40 VARIABLES
.
. Step 1--Read in the data
.
SKIP 25
READ WOMELDOR.DAT Y X F A
LET N = NUMBER Y
.
. Step 2--Plot out the data
CHARACTERS X
LINES BLANK
TITLE AUTOMATIC
TITLE OFFSET 3
.
MULTIPLOT 2 1
Y1LABEL Y; X1LABEL X; PLOT Y X
Y1LABEL F; X1LABEL A; PLOT F A
MULTIPLOT OFF
.
. Step 3--Carry out a linear fit
.
LINEAR FIT Y X
CAPTURE OUT1.
LINEAR FIT Y X
END CAPTURE
CHARACTERS X BLANK
LINES BLANK SOLID
LET RESSDYX = RESSD
SKIP 0
READ DPST1F.DAT COEF SDCOEF
LET SDA0 = SDCOEF(1)
LET SDA1 = SDCOEF(2)
X2LABEL A0 = ^A0   SD(A0) = ^SDA0   A1 = ^A1   RESSD = ^RESSDYX
LET XMAX = MAX X
Y1LABEL  Y; X1LABEL X
PLOT Y X AND
PLOT A0+A1*X FOR X = 0 1 XMAX
Y1LABEL RES; X1LABEL X; PLOT RES X
.
. Step 4A--Define the non-linear function of F = g(A)
.
LET K1 = 2*4.76
LET K2 = 12.279
.
LET FUNCTION NUM = A*(ALPHAFA+(^K2)*BETAFA*A)
LET FUNCTION DEN = (^K1)-(^K2)*BETAFA*A
LET FUNCTION RATIOFA = NUM/DEN
.
. Step 4B--Carry out the non-linear fit F = G(A)
.
LET ALPHAFA = A0
LET BETAFA = A1
FIT F = RATIOFA
CAPTURE OUT2.
FIT F = RATIOFA
END CAPTURE
LET RESSDFA = RESSD
.
SKIP 0
READ DPST1F.DAT COEFFA SDCOEFFA
LET SDALPHFA = SDCOEFFA(1)
LET SDBETAFA = SDCOEFFA(2)
X3LABEL ALPHAFA = ^ALPHAFA    SD(ALPHAFA) = ^SDALPHFA   BETAFA = ^BETAFA   RESSD = ^RESSDFA
LET AMAX = MAX A
Y1LABEL F; X1LABEL A
PLOT F A AND
PLOT RATIOFA FOR A = 0 .1 AMAX
Y1LABEL RES; X1LABEL A; PLOT RES A
.
. Step 4C--Do simulation and monitor the estimated alpha values for F = G(A)
.
FEEDBACK OFF
.
LET MEANF = MEAN F
LET SPREADF = MEANF*0.01
LET SIGMAF = SPREADF/2
.
LET MEANA = MEAN A
LET SPREADA = MEANA*0.01
LET SIGMAA = SPREADA/2
.
LET NLOOP = 100
LET FORIG = F
LET AORIG = A
.
LOOP FOR K = 1 1 NLOOP
   LET DECADE = MOD(K,10); IF DECADE = 0; WRITE "LOOP 1 ITERATION = ^K "; END IF
   LET FERROR = NORMAL RANDOM NUMBERS FOR I = 1 1 N
   LET FERROR = SIGMAF*FERROR
   LET AERROR = NORMAL RANDOM NUMBERS FOR I = 1 1 N
   LET AERROR = SIGMAA*AERROR
   LET F = FORIG+FERROR
   LET A = AORIG+AERROR
   FIT F = RATIOFA
   LET ALPHAVFA(K)=ALPHAFA
   LET RESSDVFA(K)=RESSD
END LOOP
.
LABEL
LET MEALPHFA = MEAN ALPHAVFA
LET MDALPHFA = MEDIAN ALPHAVFA
LET SDALPHFA = SD ALPHAVFA
LEGEND 1 MEAN ALPHAFA = ^MEALPHFA
LEGEND 2 MEDIAN ALPHAFA = ^MDALPHFA
LEGEND 3 SD ALPHAFA = ^SDALPHFA
HISTOGRAM ALPHAVFA
.
LET MERSDFA = MEAN RESSDVFA
LET MDRSDFA = MEDIAN RESSDVFA
LET SDRSDFA = SD RESSDVFA
LEGEND 1 MEAN RESSDFA = ^MERSDFA
LEGEND 2 MEDIAN RESSDFA = ^MDRSDFA
LEGEND 3 SD RESSDFA = ^SDRSDFA
HISTOGRAM RESSDVFA
.
LEGEND 1
LEGEND 2
LEGEND 3
.
. Step 5A--Define the non-linear function of A = h(F)
.
LET K1 = 2*4.76
LET K2 = 12.279
.
LET FUNCTION QUAD = ^K2*BETAAF
LET FUNCTION LIN = (ALPHAAF+^K2*BETAAF*F)
LET FUNCTION CONS = -^K1*F
LET FUNCTION NUM = -LIN+SQRT(LIN*LIN-4*QUAD*CONS)
LET FUNCTION DEN = 2*QUAD
LET FUNCTION RATIOAF = NUM/DEN
.
. Step 5B--Carry out the non-linear fit for A = h(F)
.
LET ALPHAAF = A0
LET BETAAF = A1
FIT A = RATIOAF
CAPTURE OUT2.
FIT A = RATIOAF
END CAPTURE
LET RESSDAF = RESSD
.
SKIP 0
READ DPST1F.DAT COEFAF SDCOEFAF
LET SDALPHAF = SDCOEFAF(1)
LET SDBETAAF = SDCOEFAF(2)
X3LABEL ALPHAAF = ^ALPHAAF    SD(ALPHAAF) = ^SDALPHAF   BETA = ^BETAAF   RESSD = ^RESSDAF
LET FMAX = MAX F
Y1LABEL A; X1LABEL F
PLOT A F AND
PLOT RATIOAF FOR F = 0 .1 FMAX
Y1LABEL RES; X1LABEL F; PLOT RES F
.
LET APRED = PRED
.
. Step 5C--Do simulation and monitor the estimated alpha values for A = h(F)
.          with A-error added to raw A's
.          and F-error added to the raw F's.
.
FEEDBACK OFF
.
LOOP FOR K = 1 1 NLOOP
   LET DECADE = MOD(K,10); IF DECADE = 0; WRITE "LOOP 2 ITERATION = ^K "; END IF
   LET FERROR = NORMAL RANDOM NUMBERS FOR I = 1 1 N
   LET FERROR = SIGMAF*FERROR
   LET AERROR = NORMAL RANDOM NUMBERS FOR I = 1 1 N
   LET AERROR = SIGMAA*AERROR
.
   LET F = FORIG+FERROR
   LET A = AORIG+AERROR
.
   FIT A = RATIOAF
   FIT A = RATIOAF
   LET ALPHAVAF(K)=ALPHAAF
   LET RESSDVAF(K)=RESSD
END LOOP
.
LABEL
X2LABEL  A' = A + A-ERROR       F' = F + F-ERROR
LET MEALPHAF = MEAN ALPHAVAF
LET MDALPHAF = MEDIAN ALPHAVAF
LET SDALPHAF = SD ALPHAVAF
LEGEND 1 MEAN ALPHAAF = ^MEALPHAF
LEGEND 2 MEDIAN ALPHAAF = ^MDALPHAF
LEGEND 3 SD ALPHAAF = ^SDALPHAF
HISTOGRAM ALPHAVAF
.
LET MERSDAF = MEAN RESSDVAF
LET MDRSDAF = MEDIAN RESSDVAF
LET SDRSDAF = SD RESSDVAF
LEGEND 1 MEAN RESSDAF = ^MERSDAF
LEGEND 2 MEDIAN RESSDAF = ^MDRSDAF
LEGEND 3 SD RESSDAF = ^SDRSDAF
HISTOGRAM RESSDVAF
.
LEGEND 1
LEGEND 2
LEGEND 3
.
. Step 5D--Do simulation and monitor the estimated alpha values for A = h(F)
.          with A-error added to the predicted line
.          and F-error added to the raw F's.
.
FEEDBACK OFF
.
LET APRED = RATIOAF
LOOP FOR K = 1 1 NLOOP
   LET DECADE = MOD(K,10); IF DECADE = 0; WRITE "LOOP 3 ITERATION = ^K "; END IF
   LET FERROR = NORMAL RANDOM NUMBERS FOR I = 1 1 N
   LET FERROR = SIGMAF*FERROR
   LET AERROR = NORMAL RANDOM NUMBERS FOR I = 1 1 N
   LET AERROR = SIGMAA*AERROR
.
.  LET F = FORIG+FERROR
.  LET A = AORIG+AERROR
   LET F = FORIG+FERROR
   LET A = APRED+AERROR
.
   FIT A = RATIOAF
   FIT A = RATIOAF
   LET ALPHAVAF(K)=ALPHAAF
   LET RESSDVAF(K)=RESSD
END LOOP
.
LABEL
X2LABEL  A' = PRED + A-ERROR       F' = F + F-ERROR
LET MEALPHAF = MEAN ALPHAVAF
LET MDALPHAF = MEDIAN ALPHAVAF
LET SDALPHAF = SD ALPHAVAF
LEGEND 1 MEAN ALPHAAF = ^MEALPHAF
LEGEND 2 MEDIAN ALPHAAF = ^MDALPHAF
LEGEND 3 SD ALPHAAF = ^SDALPHAF
HISTOGRAM ALPHAVAF
.
LET MERSDAF = MEAN RESSDVAF
LET MDRSDAF = MEDIAN RESSDVAF
LET SDRSDAF = SD RESSDVAF
LEGEND 1 MEAN RESSDAF = ^MERSDAF
LEGEND 2 MEDIAN RESSDAF = ^MDRSDAF
LEGEND 3 SD RESSDAF = ^SDRSDAF
HISTOGRAM RESSDVAF
.
LEGEND 1
LEGEND 2
LEGEND 3
.
