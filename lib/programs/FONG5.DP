. THIS IS DATAPLOT PROGRAM FILE     TESTCONT.DP
. PURPOSE--STEP THROUGH A BATTERY OF TESTS TO CHECK
.          TO SEE IF A PROCESS IS IN STATISTICAL CONTROL
.
. -----START POINT-----
.
DEVICE 2 POSTSCRIPT
.
. Step 0.1--Read in the Date
.
READ FONG5.DAT QUARTER OPEN ALL
LET PERCENTAGE = 100*(OPEN/ALL)
LET YEAR = DATA 91 91 91 91 92 92 92 92
LET QWY = DATA 1 2 3 4 1 2 3 4
.
. Step 0.2--Plot the Raw Data
.
Y1LABEL Percentage
X1LABEL Quarter
X3LABLE Plot of Raw Data
CHARACTER X
PLOT PERCENTAGE QUARTER
.
. Step 1--Carry out Linear Fit for Global Trend
.
CAPTURE JUNK1.
LINEAR FIT PERCENTAGE QUARTER
END CAPTURE
SKIP 0; READ DPST1F.DAT COEF SDCOEF
LET SDA1 = SDCOEF(2)
LET T = A1/SDA1; LET T = ROUND(T,2)
X2LABEL Slope = ^A0   SD(Slope) = ^A1   t(Slope) = ^T
X3LABEL LINEAR FIT FOR GLOBAL TREND
.
. Step 2--Carry out Linear Fit for Within-Year Trend
.
CAPTURE JUNK2.
LINEAR FIT PERCENTAGE QUARTER SUBSET YEAR 91
LINEAR FIT PERCENTAGE QUARTER SUBSET YEAR 92
END CAPTURE
CHARACTERS 91 BL 92 BL
LINES BLANK SOLID BLANK DOTTED
X3LABEL Within-Year Linear Fits
PLOT PERCENTAGE PRED VS QUARTER SUBSET YEAR 91 AND
PLOT PERCENTAGE PRED VS QUARTER SUBSET YEAR 92
.
. Step 3--Generate Autocorrelation Plot
.
CHARACTERS BLANK ALL
CHARACTERS CIRCLE
CHAR FILL ON
LINES SOLID SOLID DOT DOT DOT DOT
Y1LABEL Autocorrelation Coefficient
X1LABEL Lag
X3LABEL Autocorrelation Plot
AUTOCORRELATION PLOT PERCENTAGE
CHARACTER BLANK ALL
CHAR FILL OFF
.
. Step 4--Carry out Runs Test
.
CAPTURE JUNK3.
RUNS PERCENTAGE
END CAPTURE
.
. Step 5--Generate Normal Probability Plot
.
CHAR X
LINES SOLID
Y1LABEL Ordered Percentage
X1LABEL Normal Order Statistc Medians
X3LABEL Normal Probability Plot
NORMAL PROBABILITY PLOT PERCENTAGE
.
. Step 6--Generate P-Chart
 
CHAR CONTROL CHART
LINES CONTROL CHART
Y1LABEL Percentage
X1LABEL Quarter
X3LABEL P Control Chart
P-CHART OPEN QUARTER ALL
.
. Step 7--Generate Block Plot
.
CHARACTERS BLANK ALL
CHARACTERS 91 92
LINES SOLID ALL
LINES BLANK BLANK
XLABEL Quarter Within Year
X3LABEL Block Plot
LEGEND 1 Plot Character = Year
BLOCK PLOT PERCENTAGE QWY YEAR
LEGEND 1
.
. Step 8--Carry out t Test for Fixed Location
.
LET Y1 = Y
RETAIN Y1 SUBSET YEAR 91
LET Y2 = Y
RETAIN Y2 SUBSET YEAR 92
CAPTURE JUNK4.
T TEST Y1 Y2
END CAPTURE
.
. Step 9--Carry out F Test for Fixed Variation
.
LET S1 = SD Y1
LET S2 = SD Y2
LET FRATIO = (S1**2)/(S2**2)
LET N1 = NUMBER Y1; LET N1M1 = N1-1
LET N2 = NUMBER Y2; LET N2M1 = N2-1
LET CUTOFF = FPPF(.95,N1M1,N2M1)
.
CAPTURE JUNK5.
WRITE N1 N2 S1 S2 FRATIO CUTOFF
IF FRATIO > CUTOFF THEN
   WRITE "F Test is Significant"
END IF
IF FRATIO <= CUTOFF THEN
   WRITE "F Test is not Significant"
END IF
END CAPTURE
