. This is Dataplot macro   E691PLO5.DP
. Purpose--Generate some prelimanry graphs for an interlab analysis
.          based on the ASTM E691 standard.
.
. Updated--2015/08: Use new H CONSISTENCY PLOT to generate the plot
.
.          The following plot is generatred:
.
.          1) Plot of h-consistency statistic: Materials
.             within Laboratories
.
. Input --Y        = response variable
.       --LABID    = lab ID variable
.       --MATID    = material ID variable
.       --E691TIT  = string containing title for analysis
.
.  Step 1: Determine number of materials
.
feedback save
feedback off
let nmat = unique matid
let nlab = unique labid
let ntot = nmat*nlab
let matcode = code matid
let labcode = code labid
let alpha = 0.005
.
.  Step 2: Set plot control features
.
label case asis
title case asis
label
y1label h - Consistency Statistic
title ^e691tit: h - Materials within Laboratories
spike on
spike thickness 0.1
spike base 0
line solid all
line blank
line color black black red red
char blank blank blank blank
.
xlimits  1 ntot
major xtic mark number ntot
minor xtic mark number 0
tic offset units data
xtic offset 1 1
x1tic mark label off
title offset 2
legend 1 LAB:
legend 2 MATERIAL:
legend 1 justification right
legend 2 justification right
legend 1 coordinates 14 12
legend 2 coordinates 14 15
.
.  Step 3: Generate the plot
.
set h consistency plot materials within laboratories
h consistency plot y labcode matcode
.
.  Step 4: Add x-axis labels, grid lines
.
.
delete xcoor xcoor2 ycoor
.
movesd 87 hcv
justification left
let hcv3 = round(hcv,2)
text ^hcv3
let hcv3 = - hcv3
movesd 87 hcv3
justification left
text ^hcv3
.
let ycoor = 16
let xcoor = 1
justification center
height 1.2
if ntot > 50
   height 1
end of if
.
loop for k = 1 1 ntot
    moveds xcoor ycoor
    let ktemp = mod(k-1,nmat) + 1
    text ^ktemp
    let xcoor = xcoor + 1
end of loop
.
line color red
line dash
height 1.5
let ycoor = 12
let xcoor = (nmat/2)+0.5
loop for k = 1 1 nlab
    moveds xcoor ycoor
    text ^k
    let xcoor = xcoor + nmat
    if k < nlab
       let xcoor2 = xcoor - (nmat/2)
       drawdsds xcoor2 20 xcoor2 90
    end of if
end of loop
.
.  Step 5: Clean up
.
title
label
legend
line solid all
line color black all
char blank all
char color black all
spike off all
tic offset 0 0
x1tic mark label on
delete matcode labcode
feedback restore
.
