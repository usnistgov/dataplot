This is file contqplo.men--Generate DEX Contour Plot for Quadratic Model

--------------------
 1. DEX Contour Plot (Quadratic Model)
 2. Macro
 3.
 4. Response Variable:
 4. @CE 1 0 42 50 ? gui-vari
 5. Most Important Factor/Variable:
 5. @CE 2 0 42 50 ? gui-vari
 6. 2nd Most Important Factor/Variable:
 6. @CE 3 0 42 50 ? gui-vari
 7.
 8. Variable Containing Contour Line Values:
 8. @CE 4 0 42 50 gui-vari
 9.
10.
11. Generate the Contour Plot?:
--------------------
GR FE
PROBE MAXCOL
IF PROBEVAL < 25
  DIMENSION 25 VARIABLES
END OF IF
LET ZZZZY = _1
LET ZZZZU1 = _2
LET ZZZZU2 = _3
LET ZZZZCONT = _4
.
DELETE V1 V2
MULTIPLOT FREEZE
LIMITS -2 2
TIC OFFSET UNITS SCREEN
TIC OFFSET 0 0
LET FUNCTION Q1 = C0 + C1*V1 + C2*V2 + C12*V1*V2
LET FUNCTION Q2 = C11*V1*V1 + C22*V2*V2
LET FUNCTION Q12 = Q1+Q2
LET V1 = ZZZZU1
LET V2 = ZZZZU2
FIT ZZZZY = Q12
LET V1 = SEQUENCE -2 .4 2 FOR I = 1 1 121
LET V2 = SEQUENCE -2 11 .4 2
LET YGRID = Q12
.
LET YGRID = Q12
CONTOUR PLOT YGRID V1 V2 ZZZZCONT
.
PRE-ERASE OFF
LINES DOTTED
CHARACTER CIRCLE
CHARACTER HW 1.5 1.2
CHARACTER FILL ON
PRE-SORT OFF
LET W1 = DATA -1 1 1 -1 -1
LET W2 = DATA -1 -1 1 1 -1
PLOT W2 W1
DELETE W1 W2
.
LET NCENTER = NUMBER ZZZZY SUBSET ZZZZU1 0 SUBSET ZZZZU2 0
IF NCENTER > 0
   LET W3 = DATA 0 0
   PLOT W3 W3
   DELETE W3
END IF
.
LINES BLANK
PLOT ZZZZU2 ZZZZU1
.
LET YMM = MEAN ZZZZY SUBSET ZZZZU1 -1 SUBSET ZZZZU2 -1
LET YPM = MEAN ZZZZY SUBSET ZZZZU1 1 SUBSET ZZZZU2 -1
LET YMP = MEAN ZZZZY SUBSET ZZZZU1 -1 SUBSET ZZZZU2 1
LET YPP = MEAN ZZZZY SUBSET ZZZZU1 1 SUBSET ZZZZU2 1
JUST RIGHT; MOVEDATA -1.1 -1.2; TEXT ^YMM
JUST LEFT; MOVEDATA +1.1 -1.2; TEXT ^YPM
JUST RIGHT; MOVEDATA -1.1 +1.1; TEXT ^YMP
JUST LEFT; MOVEDATA +1.1 +1.1; TEXT ^YPP
.
LET YWEST = MEAN ZZZZY SUBSET ZZZZU1 -1.414 SUBSET ZZZZU2 0
LET YNORTH = MEAN ZZZZY SUBSET ZZZZU1 0 SUBSET ZZZZU2 +1.414
LET YEAST = MEAN ZZZZY SUBSET ZZZZU1 +1.414 SUBSET ZZZZU2 0
LET YSOUTH = MEAN ZZZZY SUBSET ZZZZU1 0 SUBSET ZZZZU2 -1.414
JUST RICE; MOVEDATA -1.5 -0; TEXT ^YWEST
JUST CENTER; MOVEDATA 0 +1.5; TEXT ^YNORTH
JUST LECE; MOVEDATA +1.5 0; TEXT ^YEAST
JUST CENTER; MOVEDATA 0 -1.6; TEXT ^YSOUTH
.
IF NCENTER > 0
   LET YCP = MEAN ZZZZY SUBSET ZZZZU1 0 SUBSET ZZZZU2 0
   JUST LEFT; MOVEDATA +0.1 +0.1; TEXT ^YCP
END IF
.
IF NCENTER > 1.5
   LET MCENTER = MEAN ZZZZY SUBSET ZZZZU1 0 SUBSET ZZZZU2 0
   LET SDCENTER = SD ZZZZY SUBSET ZZZZU1 0 SUBSET ZZZZU2 0
   LET NEDGE = NUMBER ZZZZY EXCEPT ZZZZU1 0 EXCEPT ZZZZU2 0
   LET MEDGE = MEAN ZZZZY EXCEPT ZZZZU1 0 EXCEPT ZZZZU2 0
   LET STATNUM = MEDGE-MCENTER
   LET STATDEN = SDCENTER*SQRT((1/NEDGE)+(1/NCENTER))
   LET TESTSTAT = STATNUM/STATDEN
   LET NDF = NCENTER-1
   LET CUTOFF2 = TPPF(.975,NDF)
   LET CUTOFF1 = -CUTOFF2
.
   LET STRING CONCLUSION = NO CURVATURE
   IF TESTSTAT < CUTOFF1
      LET STRING CONCLUSION = CURVATURE
   END IF
   IF TESTSTAT > CUTOFF2
      LET STRING CONCLUSION = CURVATURE
   END IF
END IF
.
IF NCENTER > 1.5
   JUSTIFICATION CENTER
   MOVE 50 11
   TEXT Center: Num. Obs. = ^NCENTER  Average = ^MCENTER  SD = ^SDCENTER
   MOVE 50 8
   TEXT Edge: Num. Obs. = ^NEDGE   Average = ^MEDGE
   MOVE 50 5
   TEXT Curvature Check:  t Test Stat Value = ^TESTSTAT
   MOVE 50 2
   TEXT t^NDF Lower Cutoff = ^CUTOFF1   t^NDF Upper Cutoff = ^CUTOFF2
END IF
.
. STEP 7--CLEAN UP
.
MULTIPLOT UNFREEZE
.
LIMITS
TIC OFFSET UNITS SCREEN
TIC OFFSET UNITS 5 5
PRE-ERASE ON
LINES SOLID ALL
CHARACTERS BLANK ALL
CHARACTER FILL OFF
PRE-SORT ON
--------------------
WEB HELP DEX CONTOUR PLOT
--------------------
