. THIS IS THE DATAPLOT PROGRAM FILE  POWEREST.DP
. PURPOSE - FIT POWER LAW MODEL WITH MTBF CONFIDENCE BOUNDS
. DATE FEBRUARY 19,1998
.
. ------------------ START POINT  -------------------
FEEDBACK OFF
SET IO TERMINAL
WRITE " "
WRITE "THIS PROGRAMS ASSUMES YOU HAVE ALREADY"
WRITE "CREATED THE FOLLOWING VARIABLES"
WRITE " "
WRITE "     T = THE TOTAL SYSTEM TEST TIME"
WRITE "     R = THE TOTAL NUMBER OF FAILURES (R GREATER THAN 2)"
WRITE "     TIMES = A DATA VECTOR OF THE R SYSTEM FAILURE TIMES" 
WRITE " "
WRITE "DO YOU WANT TO CONTINUE?"
WRITE " " 
WRITE "IF NO, ENTER 0"
WRITE "IF YES, ENTER 1"
READ PARAMETER DEC
IF DEC > 0
.
.
DELETE RELBETA BET A1 B1 MTBF ML MU ZAL ALPHA CAL1 CAL2 CAL3 CAL4 CAL5
WRITE " "
WRITE "INPUT YOUR ALPHA FOR 100 X (1-ALPHA) CONFIDENCE INTERVAL"
READ PARAMETER ALPHA
SET IO
LET BET = LOG(T/TIMES)
LET B1 = SUM BET
LET B1 = (R-1)/B1
LET RELBETA = 1 - B1
LET A1 = R/(T**B1)
LET MTBF = T/(R*B1)
SET WRITE DECIMALS 3
WRITE "THE RELIABILITY GROWTH SLOPE BETA IS ^RELBETA "
WRITE " "
WRITE "THE A PARAMETER IS ^A1 "
WRITE " "
WRITE "THE MTBF AT END OF TEST IS ^MTBF " 
WRITE " "
LET ZAL = NORPPF(1-.5*ALPHA)
LET CAL1 = R*(ZAL**2)
LET CAL1 = CAL1/2
LET CAL2 = (ZAL**4)/16
LET CAL3 = CAL1+CAL2
LET CAL3 = CAL3**.5
LET CAL4 = (ZAL**2)/4
LET CAL5 = R + CAL4 + CAL3
LET CAL5 = CAL5**2
LET ML = MTBF*R*(R-1)
LET ML = ML/CAL5
LET CAL1 = (R/2)**.5
LET CAL2 = R - ZAL*CAL1
LET CAL2 = CAL2**2
LET MU = MTBF*R*(R-1)
LET MU = MU/CAL2
LET FACLOW = ML/MTBF
LET FACLOW = FACLOW*(R/(R+1))
LET FACUP = MU/MTBF
LET FACUP = FACUP*(R/(R+1))
LET BOUND1 = 100*(1-ALPHA)
LET BOUND2 = 100*(1-.5*ALPHA)
WRITE "THE DESIRED ^BOUND1 PERCENT CONFIDENCE INTERVAL IS: (^ML , ^MU )"
WRITE "AND ^ML IS A (ONE-SIDED) ^BOUND2 PERCENT LOWER LIMIT"     
END IF   
FEEDBACK ON
