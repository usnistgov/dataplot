----- <~dataloex\KRASNY1.DP> Load & Execute KRASNY1.DAT

. This is file    KRASNY1.DP

. CIGARETTE BURN PROPENSITY (JOHN KRASNY / RICHARD GANN / KEITH EBERHARDT)
. 60 MINUTES--MARCH 27, 1994
. COMPLETE ANALYSIS OF 2**5 DESIGN
. INPUT DATA FILE--KRANSY1.DAT
. OUTPUT FILES--DPPL1F.DAT (A POSTSCRIPT FILE CONTAINING ALL PLOT OUTPUT)
.               OUT1. (YATES ANALYSIS OUTPUT)
.               OUT2. (PREDICTED VALUES AND RESIDUALS FROM FINAL MODEL)
.               OUT3. (SORTED Y AND CARRY-ALONG X'S)
.
. STEP 1--READ IN THE DATA
.
DELETE Y Y1 Y2 Y3 Y4 Y5 JUNK X1 X2 X3 X4 X5
SKIP 25
READ KRASNY1.DAT Y1 TO Y9 X1 TO X5
TITLE KRASNY1.DAT
LET Y = Y5
.
. STEP 2--GENERATE DEX SCATTER PLOT & DEX MEAN PLOT
.
MULTIPLOT CORNER COORDINATES 5 5 95 95
DEVICE 1 FONT SIMPLEX
MULTIPLOT 2 1
LABEL SIZE 4
X3LABEL AUTOMATIC
CHAR X; LINES BLANK
DEX SCATTER PLOT Y X1 X2 X3 X4 X5
CHAR BLANK; LINES SOLID
DEX MEAN PLOT Y X1 X2 X3 X4 X5
LINES SOLID ALL; LINES BLANK BLANK
.
. STEP 3--GENERATE MATRIX OF MEAN PLOTS (USE DEXSTAT2.DP MACRO)
.
MULTIPLOT OFF
SLEEP 3
ERASE
X3LABEL
LET NUMFAC = 5
LET DP = 1
LET STRING STAT = MEAN
YLIMITS 8 20
LET STRING S0 = BURN PROPENSITY
LET STRING S1 = CITRATE CONC (X1)
LET STRING S2 = PAPER POROSITY (X2)
LET STRING S3 = TOB. EXPANSION (X3)
LET STRING S4 = TOBACCO TYPE (X4)
LET STRING S5 = CIRCUMFERENCE (X5)
CALL DEXSTAT2.DP
YLIMITS
SLEEP 3
ERASE
.
. STEP 4--GENERATE BLOCK PLOTS
.
MULTIPLOT 3 2
CHAR 1 2; LINES BLANK BLANK
CHAR SIZE 5 ALL; LABEL SIZE 5; TIC LABEL SIZE 5
X3LABEL AUTOMATIC
X1TIC LABELS OFF
X1TICS OFF
BAR EXPANSION 2 1.5
YLIMITS 0 20
BLOCK PLOT Y X2 X3 X4 X5 X1
BLOCK PLOT Y X1 X3 X4 X5 X2
BLOCK PLOT Y X1 X2 X4 X5 X3
BLOCK PLOT Y X1 X2 X3 X5 X4
BLOCK PLOT Y X1 X2 X3 X4 X5
YLIMITS
BAR EXPANSION 1 1
MULTIPLOT OFF
CHAR SIZE 2 ALL; LABEL SIZE 2; TIC LABEL SIZE 2
X1TICS ON
X1TIC LABELS ON
.
. STEP 5--DO YATES ANALYSIS (DATA MUST BE IN YATES ORDER)
.         NOTE--COEFFICIENTS STORED OUT TO FILE  DPST1F.DAT
.
CAPTURE OUT1.
YATES Y
END CAPTURE
SLEEP 3
ERASE
.
. STEP 6--GENERATE NORMAL AND HALFNORMAL PROBABILITY PLOTS ON THE EFFECTS
.         NOTE--MUST READ COEFFICIENTS IN FROM FILE  DPST1F.DAT
.
SKIP 0
READ DPST1F.DAT TAG COEF T RESSD1 RESSD2
WRITE TAG COEF T RESSD1 RESSD2
CHAR X; LINES BLANK
LABEL SIZE 4
X3LABEL AUTOMATIC
MULTIPLOT 2 2
NORMAL PROBABILITY PLOT COEF
LET ABSCOEF = ABS(COEF)
HALFNORMAL PROBABILITY PLOT ABSCOEF
.
. STEP 7--GENERATE PREDICTED VALUES AND RESIDUALS FOR PARTIAL MODEL
.
LET MU = MEAN Y
LET B3 = COEF(1)
LET B2 = COEF(2)
LET B5 = COEF(3)
LET B45 = COEF(4)
LET B13 = COEF(5)
LET RESSD = RESSD2(5)
. LET PRED = MU+0.5*(B1*X1+B2*X2)
LET PRED = MU + 0.5*(B3*X3+B2*X2+B5*X5+B45*X4*X5+B13*X1*X3)
LET RES = Y-PRED
SET WRITE DECIMALS 2
WRITE X1 X2 X3 X4 X5 Y PRED RES
WRITE OUT2. X1 X2 X3 X4 X5 Y PRED RES
.
. STEP 8--GENERATE NORMAL PROBABILITY PLOT OF RESIDUALS
.
X1LABEL PREDICTED Y = ^MU+0.5*(^B3*X3+^B2*X2+^B5*X5+^B45*X4*X5+^B13*X1*X3)
X2LABEL RESIDUAL STANDARD DEVIATION = ^RESSD
X3LABEL
LABEL SIZE 3
PLOT RES
NORMAL PROBABILITY PLOT RES
X1LABEL
X2LABEL
LABEL SIZE 2
MULTIPLOT OFF
SLEEP 3
ERASE
.
. STEP 9--GENERATE CONTOUR PLOT FOR THE 2 MOST IMPORTANT FACTORS
.          (USE MACRO DEXCONT.DP)
.
DELETE Y2
LET YCONT = SEQUENCE 0 2 20
LET U1 = X3
LET U2 = X2
CHAR BLANK ALL; LINES SOLID ALL
X1LABEL TOBACCO EXPANSION (X3)
Y1LABEL PAPER POROSITY (X2)
X3LABEL
CALL DEXCONT.DP
X1LABEL
Y1LABEL
SLEEP 3
ERASE
.
. STEP 10--SORT Y AND CARRY ALONG X... FACTORS
.
LET Y2 = SORTC Y X1 X2 X3 X4 X5
SET WRITE DECIMALS 0
WRITE X1 X2 X3 X4 X5 Y2
WRITE OUT3. X1 X2 X3 X4 X5 Y2
.
WINDOW CORNER COORDINATES 20 20 83 60
XLIMITS 0 32
CHARACTERS X
LINES BLANK
PLOT Y2
XLIMITS
DEVICE 1 FONT TEKTRONIX
RESET PLOT CONTROL
CAPTURE ZZZZJUNK.DAT
WRITE "THE FILES: OUT1., OUT2., AND OUT3. CONTAIN THE"
WRITE "QUANTITIATIVE OUTPUT FROM THE YATES ANALYSIS."
WRITE "ENTER THE COMMANDS:"
WRITE "     LIST OUT1."
WRITE "     LIST OUT2."
WRITE "     LIST OUT3."
WRITE "TO VIEW THEM"
END OF CAPTURE
LIST ZZZZJUNK.DAT
